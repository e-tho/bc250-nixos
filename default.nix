{ config, lib, ... }:

let
  cfg = config.hardware.bc250;
in
{
  imports = [
    ./aic8800d80/module.nix
    ./bc250-acpi-fix/module.nix
    ./bc250-amdgpu/module.nix
    ./bc250-core-unlock/module.nix
    ./bc250-cu-live-manager/module.nix
    ./bc250-memcfg/module.nix
    ./bc250-mesa/module.nix
    ./cyan-skillfish-governor-smu/module.nix
    ./bc250-smu-oc/module.nix
    ./bc250-smu-patch/module.nix
  ];

  options.hardware.bc250 = {
    enable = lib.mkEnableOption "BC-250 board support";

    features = {
      aic8800d80.enable = lib.mkEnableOption "AIC8800D80 Wi-Fi/Bluetooth support";
      sensors.enable = lib.mkEnableOption "nct6687 sensor support" // { default = true; };
      cuLiveManager.enable = lib.mkEnableOption "BC-250 CU live manager";
      acpiFix.enable = lib.mkEnableOption "ACPI table overrides for CPU idle states and frequency scaling";
      coreUnlock.enable = lib.mkEnableOption "BC-250 CPU core unlock (6c/12t to 8c/16t)";
      smuPatch.enable = lib.mkEnableOption "Apply BC-250 SMU SRAM patches (unlock + RPC + 8-core metrics) at boot";
      vramSplit = lib.mkOption {
        type = lib.types.nullOr lib.types.int;
        default = null;
        example = 512;
        description = "Static VRAM/UMA split in MB to write with bc250memcfg. This value represents the minimum amount of memory that will be allocated to VRAM, however additional memory can be dynamically allocated. Null leaves CMOS unchanged.";
      };
      vramDynamicSplit = lib.mkOption {
        type = lib.types.nullOr lib.types.int;
        default = null;
        example = 4096;
        description = "How much additional memory can be dynamically allocated to VRAM, in MB (converted to 4 KiB pages and passed as ttm.pages_limit). Null leaves it unset. If unset, the linux kernel typically sets this value to 1/2 of the RAM allocation.";
      };
      gpuGovernor.enable = lib.mkEnableOption "Cyan Skillfish GPU governor" // { default = true; };
      cpuOverclock.enable = lib.mkEnableOption "Enable CPU Overclocking service";
      cpuOverclock.configFile = lib.mkOption {
        type = lib.types.path;
        default = "/etc/overclock.conf";
        description = "Path to the generated overclock configuration file.";
      };
      zswap.enable = lib.mkEnableOption "recommended zswap settings" // { default = true; };
      gpuPatches = {
        enable = lib.mkEnableOption "amdgpu and Mesa patches for the BC-250";
        cuUnlock.enable = lib.mkEnableOption "40 CU unlock";
      };
    };
  };

  config = lib.mkIf cfg.enable (lib.mkMerge [
    {
      services.bc250-memcfg.enable = true;

      assertions = [
        {
          assertion = cfg.features.vramSplit == null || cfg.features.vramSplit > 0;
          message = "hardware.bc250.features.vramSplit must be null or a positive MB value.";
        }
        {
          assertion = cfg.features.vramDynamicSplit == null || cfg.features.vramDynamicSplit > 0;
          message = "hardware.bc250.features.vramDynamicSplit must be null or a positive MB value.";
        }
      ];
    }

    (lib.mkIf cfg.features.aic8800d80.enable {
      hardware.aic8800d80.enable = lib.mkDefault true;
    })

    (lib.mkIf cfg.features.sensors.enable {
      boot.extraModulePackages = [ config.boot.kernelPackages.nct6687d ];
      boot.kernelModules = [ "nct6687" ];
      boot.extraModprobeConfig = ''
        options nct6687 force=true
      '';
    })

    (lib.mkIf cfg.features.cuLiveManager.enable {
      services.bc250-cu-live-manager.enable = lib.mkDefault true;
    })

    (lib.mkIf cfg.features.acpiFix.enable {
      services.bc250-acpi-fix.enable = lib.mkDefault true;
    })

    (lib.mkIf cfg.features.coreUnlock.enable {
      services.bc250-core-unlock.enable = lib.mkDefault true;
    })

    (lib.mkIf cfg.features.smuPatch.enable {
      services.bc250-smu-patch.enable = lib.mkDefault true;
    })

    (lib.mkIf (cfg.features.vramSplit != null) {
      services.bc250-memcfg.applyOnActivation = true;
      services.bc250-memcfg.applyOnBoot = true;
      services.bc250-memcfg.settings.UMA_SIZE = lib.mkDefault cfg.features.vramSplit;
    })

    (lib.mkIf (cfg.features.vramDynamicSplit != null) {
      boot.kernelParams = [
        "ttm.pages_limit=${toString (cfg.features.vramDynamicSplit * 256)}"
      ];
    })

    (lib.mkIf cfg.features.gpuGovernor.enable {
      services.cyan-skillfish-governor-smu.enable = lib.mkDefault true;
    })

    (lib.mkIf cfg.features.cpuOverclock.enable {
      services.bc250-cpu-oc.enable = lib.mkDefault true;
      services.bc250-cpu-oc.configFile = cfg.features.cpuOverclock.configFile;
    })

    (lib.mkIf cfg.features.zswap.enable {
      boot.kernel.sysctl = {
        "vm.swappiness" = lib.mkDefault 180;
      };
      boot.zswap = {
        enable = lib.mkDefault true;
        compressor = lib.mkDefault "lz4";
      };
    })

    (lib.mkIf cfg.features.gpuPatches.enable {
      hardware.bc250-amdgpu.enable = lib.mkDefault true;
      hardware.bc250-amdgpu.cuUnlock.enable =
        lib.mkDefault cfg.features.gpuPatches.cuUnlock.enable;
      hardware.bc250-mesa.enable = lib.mkDefault true;
    })
  ]);
}
